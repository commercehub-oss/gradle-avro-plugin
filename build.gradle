buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
        classpath "com.github.townsfolk:gradle-release:1.2"
    }
}

apply plugin: "groovy"
apply plugin: "idea"

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.apache.avro:avro-compiler:1.7.7"
    compile "org.apache.commons:commons-io:1.3.2"
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:18.0'
}

sourceCompatibility = 1.7

group = "com.commercehub.gradle.plugin"

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

apply plugin: "maven-publish"
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

}

apply plugin: "release"
apply plugin: "bintray"
def project = this
bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = "main"
        userOrg = "commercehub-oss"
        name = project.name
        licenses = ["Apache-2.0"]
    }
}
bintrayUpload.dependsOn build, sourcesJar, javadocJar
createReleaseTag.dependsOn bintrayUpload
